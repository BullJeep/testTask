
&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	ЭтотОбъект.ДатаКурса = ТекущаяДатаСеанса();
	Если ЭтотОбъект.ТаблицаПересчетаВалют.Количество() = 0 Тогда
		НоваяСтрокаUSD = ЭтотОбъект.ТаблицаПересчетаВалют.Добавить();
		НоваяСтрокаUSD.Валюта = "USD";
		НоваяСтрокаEUR = ЭтотОбъект.ТаблицаПересчетаВалют.Добавить();
		НоваяСтрокаEUR.Валюта = "EUR";
	КонецЕсли;
КонецПроцедуры


&НаКлиенте
Процедура Пересчитать(Команда)
	ПересчитатьНаСервере();
КонецПроцедуры

&НаСервере
Процедура ПересчитатьНаСервере()
	//https://bank.gov.ua/NBUStatService/v1/statdirectory/exchange?valcode=EUR&date=20200302&json
	
	СоединениеНБУ = Новый HTTPСоединение("bank.gov.ua", , , , , , Новый ЗащищенноеСоединениеOpenSSL(), );
	
	ЗаголовокЗапроса = Новый Соответствие;
	ЗаголовокЗапроса.Вставить("Content-Type", "text/json; charset=utf-8");
	
	ЗапросНБУ = Новый HTTPЗапрос;
	
	Для каждого СтрокаТаблицыЗначений ИЗ ЭтотОбъект.ТаблицаПересчетаВалют Цикл
		СтрокаТаблицыЗначений.Курс = 0;
		СтрокаТаблицыЗначений.Сумма = 0;
		
		СтрокаЗапроса = "/NBUStatService/v1/statdirectory/exchange?json";
		СтрокаЗапроса = СтрокаЗапроса + "&valcode="+СокрЛП(СтрокаТаблицыЗначений.Валюта);
		СтрокаЗапроса = СтрокаЗапроса + "&date="+Формат(ЭтотОбъект.ДатаКурса, "ДФ=yyyyMMdd");
		
		ЗапросНБУ.АдресРесурса = СтрокаЗапроса;
		ЗапросНБУ.Заголовки = ЗаголовокЗапроса;
		ОтветНБУ = СоединениеНБУ.Получить(ЗапросНБУ);
		Если ОтветНБУ.КодСостояния <> 200 Тогда
			Продолжить;	
		КонецЕсли;
	
		ОтветСтрокой = ОтветНБУ.ПолучитьТелоКакСтроку();
		Если Не ЗначениеЗаполнено(ОтветСтрокой) Тогда 
			Продолжить;
		КонецЕсли;	
		
		ЧтениеJSON = Новый ЧтениеJSON;
		ЧтениеJSON.УстановитьСтроку(ОтветСтрокой);
			
		ДанныеНБУ = ПрочитатьJSON(ЧтениеJSON, Ложь);
		
		Если Не ЗначениеЗаполнено(ДанныеНБУ[0]) Тогда
			Продолжить;
		КонецЕсли;	
		
		СтрокаТаблицыЗначений.ДатаКурса = ЭтотОбъект.ДатаКурса;
		СтрокаТаблицыЗначений.Курс = ДанныеНБУ[0].rate;
		СтрокаТаблицыЗначений.Сумма = ЭтотОбъект.Сумма / СтрокаТаблицыЗначений.Курс;
		
	КонецЦикла;
КонецПроцедуры

&НаКлиенте
Процедура Очистить(Команда)
	ЭтотОбъект.ТаблицаПересчетаВалют.Очистить();
КонецПроцедуры

